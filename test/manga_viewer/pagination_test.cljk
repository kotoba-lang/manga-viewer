(ns manga-viewer.pagination-test
  (:require [clojure.test :refer [deftest is testing]]
            [manga-viewer.pagination :as p]))

(def single-image-work
  {:manga/id "w" :manga/title "W"
   :manga/pages (mapv (fn [n] {:page/number n :page/images [(str "p" n)]})
                      (range 1 8))})

(def panel-work
  {:manga/id "w2" :manga/title "W2"
   :manga/pages [{:page/number 1 :page/images ["a" "b"]}
                 {:page/number 2 :page/images ["c"]}]})

(deftest spread-units-follow-rtl-print-convention
  (testing "cover alone, then pairs (7 pages, manga.gftd.ai parity)"
    (is (= [[0] [1 2] [3 4] [5 6]] (p/units 7 :spread))))
  (testing "even count leaves a trailing single page"
    (is (= [[0] [1 2] [3]] (p/units 4 :spread))))
  (is (= [] (p/units 0 :spread))))

(deftest single-and-scroll-units
  (is (= [[0] [1] [2]] (p/units 3 :single)))
  (is (= [[0 1 2]] (p/units 3 :scroll))))

(deftest supported-modes-depend-on-page-image-shape
  (is (= [:spread :single :scroll] (p/supported-modes single-image-work)))
  (is (= [:single :scroll] (p/supported-modes panel-work))
      "panel-per-image works cannot spread-pair"))

(deftest clamping-and-position
  (let [us (p/units 7 :spread)]
    (is (= 0 (p/clamp-unit us -3)))
    (is (= 3 (p/clamp-unit us 99)))
    (is (= 2 (p/unit-for-page us 4)) "page index 4 lives in unit [3 4]")
    (is (= "4-5 / 7" (p/unit-label us 2 7)))
    (is (= "1 / 7" (p/unit-label us 0 7)))))
